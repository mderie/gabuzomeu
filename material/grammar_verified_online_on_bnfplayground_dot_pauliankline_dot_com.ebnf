; In order to play with a specific part of the grammar
; Just put this optional rule on this site :
; https://bnfplayground.pauliankline.com/
<totest> ::= <expression>






<label> ::= [A-Z]+
<cell> ::= "GA" | "BU" | "ZO" | "MEU"

<litteral> ::= "#" <cell>+
<colon_label> ::= ":" <label>

<program> ::= <line>*
<line> ::= <colon_label> | <colon_label> <instruction> | <instruction>
<instruction> ::= <last> | <jump> | <dump> | <pump> | <free> | <bird> | <move> | <calc> | <head> | <tail> | <zero> | <else>
<last> ::= "LAST" <label>
<jump> ::= "JUMP" <label>
<dump> ::= "DUMP" <cell>
<pump> ::= "PUMP" <cell>
<free> ::= "FREE" <cell>
<bird> ::= "BIRD" <cell>
<move> ::= "MOVE" <cell>
<head> ::= "HEAD" <cell> "," <label>
<tail> ::= "TAIL" <cell> "," <label>
<zero> ::= "ZERO" <cell> "," <label>
<else> ::= "ELSE" <cell> "," <label>

<calc> ::= "CALC" <cell> "," <expression>
<expression> ::= <term> (("+" | "-") <term>)*
<term> ::= <power> (("*" | "/" | "%") <power>)*
<power> ::= <factor> ("^" <factor>)*
<factor> ::=  <litteral> | <cell> | "(" <expression> ")"